#!/bin/zsh
#
# A minimal .zshrc
#
# Author: Sam Clements <sam@borntyping.co.uk>
# Licence: MIT
#

ZSH_CACHE=$HOME/.cache/zsh                      # A cache directory for use in other options
setopt INTERACTIVE_COMMENTS                     # Allow comments in interactive mode
setopt RM_STAR_WAIT                             # Force the user to wait when doing `rm *`

#
# History settings
#

SAVEHIST=10000                                  # Lines of history to save to a file
HISTSIZE=10000                                  # Lines of history to keep in memory
HISTFILE=$HOME/.cache/zsh/history               # Save history in .cache

setopt APPEND_HISTORY                           # Append to the HISTFILE, not replace
setopt HIST_IGNORE_DUPS                         # Ignore duplicate history entries
setopt SHARE_HISTORY                            # Share history between ZSH instances

#
# Completion
#

autoload -Uz compinit && compinit               # Loads completion modules
setopt AUTO_MENU                                # Show completion menu on succesive tab press
setopt ALWAYS_TO_END                            # Move cursor after completion
setopt COMPLETE_ALIASES                         # Allow autocompletion for aliases
setopt COMPLETE_IN_WORD                         # Allow completion from middle of word
setopt LIST_PACKED                              # Smallet completion menu

# Intelligent handling of characters after a completion
setopt AUTO_PARAM_KEYS AUTO_PARAM_SLASH AUTO_REMOVE_SLASH

zstyle ':completion:*' list-colors ''
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path "$ZSH_CACHE/completion/"
zstyle ':completion:*:*:*:users' ignored-patterns '*'

# Fuzzy completion - case insensitive, partial-word and substring
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

#
# Prompt
#

autoload -U vcs_info                            # Version control information
autoload -U colors && colors                    # Adds variables for printing colors
setopt PROMPT_SUBST                             # Allow substitution in PROMPT

# Set styles for vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true 
zstyle ':vcs_info:*' check-for-staged-changes true
zstyle ':vcs_info:*' stagedstr "%{$fg[green]%}!%{$reset_color%}"
zstyle ':vcs_info:*' unstagedstr "%{$fg[yellow]%}?%{$reset_color%}"
zstyle ':vcs_info:*' formats "%{$fg_bold[green]%}%s:%{$fg_bold[green]%}%b%{$reset_color%}%c%u " 
zstyle ':vcs_info:*' actionformats "%s:%b(%a)%c%u "

# Load virtualenvwrapper, and stop virtualenv from modifying the prompt
if [[ -f /usr/share/virtualenvwrapper/virtualenvwrapper.sh ]]; then
    export VIRTUALENVWRAPPER_PYTHON=/usr/bin/python
    export VIRTUAL_ENV_DISABLE_PROMPT=1
    source /usr/share/virtualenvwrapper/virtualenvwrapper.sh
fi

# Display the current virtualenv when active
function env_info() {
    env_info_msg=""
    if [ -n "$VIRTUAL_ENV" ]; then
        env_info_msg="%{$fg_bold[green]%}virtualenv:$(basename "$VIRTUAL_ENV")%{$reset_color%} "
    fi
}

function precmd() { print -Pn "\e]2;%~\a"; vcs_info; env_info; }
function preexec() { print -Pn "\e]2;%~ $ $2\a"; }

PROMPT='\
%{$fg_bold[green]%}%n@%m%{$reset_color%} \
%{$fg_bold[blue]%}%~%{$reset_color%} \
${vcs_info_msg_0_}${env_info_msg}\
%{$fg_bold[blue]%}$%{$reset_color%} '

#
# Named directories 
#

setopt AUTO_NAME_DIRS                           # Automatically create named directories

borntyping="$HOME/Development/borntyping"       # GitHub organisations
sandbox="$HOME/Development/borntyping-sandbox"  # Experimental projects

personal="$HOME/Development/personal"           # Private repositories
university="${personal}/university"             # University repository
mmp="${university}/CS39440"                     # Major project directory

#
# Aliases
#

alias ls='ls --color=auto'                      # Use colors with ls
alias grep='grep --color=auto'                  # Use colors for grep output
alias -g ...='../..'                            # Allow ... to mean ../.. anywhere
alias gc='git commit -v -a'                     # Show diff in commit editor, commit everything
alias gd='git diff'
alias gs='git status'

# Set colors to use with `ls --color`
export LS_COLORS='rs=0:di=01;34:ln=target:or=41:ex=32'

#
# Bin
#

function add_to_path() {
    [[ -d $1 ]] && export PATH="$1:$PATH"
}

add_to_path "$HOME/.bin"                        # Used for personal scripts
add_to_path "$HOME/.local/bin"                  # Used by pipsi for commands
add_to_path "$HOME/.gem/ruby/1.9.1/bin"         # Used by Ruby 1.9.1 for user gems
add_to_path "$HOME/.gem/ruby/2.0.0/bin"         # Used by Ruby 2.0.0 for user gems

# RVM
# https://rvm.io/

function use_rvm() {                            # Avoid loading RVM for every shell
    source "$HOME/.rvm/scripts/rvm"             # Load RVM using `source`, as RVM provides a shell
    add_to_path "$HOME/.rvm/bin"                #   function that modifies the environment
    rvm use                                     # Start using RVM
}

# z directory script
# https://github.com/rupa/z

if [[ -f /usr/local/bin/z.sh ]]; then
    source /usr/local/bin/z.sh
fi
