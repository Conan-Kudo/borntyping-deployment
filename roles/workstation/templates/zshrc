#!/bin/zsh
#
# A minimal .zshrc
#
# Author: Sam Clements <sam@borntyping.co.uk>
# Licence: MIT
#

ZSH_CACHE=$HOME/.cache/zsh                      # A cache directory for use in other options
setopt INTERACTIVE_COMMENTS                     # Allow comments in interactive mode
setopt RM_STAR_WAIT                             # Force the user to wait when doing `rm *`

#
# History settings
#

SAVEHIST=10000                                  # Lines of history to save to a file
HISTSIZE=10000                                  # Lines of history to keep in memory
HISTFILE=$ZSH_CACHE/history                     # Save history in .cache

setopt APPEND_HISTORY                           # Append to the HISTFILE, not replace
setopt HIST_IGNORE_DUPS                         # Ignore duplicate history entries
setopt INC_APPEND_HISTORY                       # Append history immediately 
setopt SHARE_HISTORY                            # Share history between ZSH instances
setopt EXTENDED_HISTORY                         # Store more information on history items

#
# Completion
#

autoload -Uz compinit && compinit               # Loads completion modules
setopt AUTO_MENU                                # Show completion menu on succesive tab press
setopt ALWAYS_TO_END                            # Move cursor after completion
setopt COMPLETE_ALIASES                         # Allow autocompletion for aliases
setopt COMPLETE_IN_WORD                         # Allow completion from middle of word
setopt LIST_PACKED                              # Smallet completion menu

# Intelligent handling of characters after a completion
setopt AUTO_PARAM_KEYS AUTO_PARAM_SLASH AUTO_REMOVE_SLASH

zstyle ':completion:*' list-colors ''
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path $ZSH_CACHE/completion/
zstyle ':completion:*:*:*:users' ignored-patterns '*'

#
# Title
#

function precmd() { print -Pn "\e]2;%n@%m\a" }  # Set title to user@host
function preexec() { print -Pn "\e]2;$2\a"; }   # Set title to the current command

#
# Prompt
#

autoload -U vcs_info                            # Version control information
autoload -U colors && colors                    # Adds variables for printing colors
setopt PROMPT_SUBST                             # Allow substitution in PROMPT

# Load virtualenvwrapper, and display the current virtualenv in the prompt
if [[ -f /usr/local/bin/virtualenvwrapper.sh ]]; then
    export VIRTUALENVWRAPPER_PYTHON=/usr/bin/python3.4
    export VIRTUAL_ENV_DISABLE_PROMPT=1
    source /usr/local/bin/virtualenvwrapper.sh
fi

# Display the current virtualenv when active
function prompt_venv() {
    [ -n "$VIRTUAL_ENV" ] && echo "virtualenv:$(basename "$VIRTUAL_ENV") "
}

PROMPT='\
%{$fg_bold[green]%}%n@%m%{$reset_color%} \
%{$fg_bold[blue]%}%~%{$reset_color%} \
${vcs_info_msg_0_}$(prompt_venv)\
%{$fg_bold[blue]%}$%{$reset_color%} '

#
# Named directories 
#

setopt AUTO_NAME_DIRS                           # Automatically create named directories
borntyping="$HOME/Development/borntyping"       # http://github.com/borntyping
phoenix_ci="$HOME/Development/phoenix-ci"       # http://github.com/phoenix-ci
personal="$HOME/Development/personal"           # Unpublished projects

#
# Aliases
#

alias ls='ls --color=auto'                      # Use colors with ls
alias grep='grep --color=auto'                  # Use colors for grep output
alias -g ...='../..'                            # Allow ... to mean ../.. anywhere
alias gs='git status'
alias gd='git diff'

# Set colors to use with `ls --color`
export LS_COLORS='rs=0:di=01;34:ln=target:or=41:ex=32'

#
# Bin
#

export PATH="$HOME/.gem/ruby/1.9.1/bin:$PATH"
export PATH="$HOME/bin:$PATH"
